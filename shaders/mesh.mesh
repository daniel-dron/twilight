#version 460

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_mesh_shader : require
#extension GL_ARB_gpu_shader_int64 : enable

#include "mesh.glsl"
#include "draws.glsl"

layout( local_size_x = 32, local_size_y = 1, local_size_z = 1 ) in;
layout( triangles, max_vertices = 64, max_primitives = 126 ) out;

layout( push_constant, scalar ) uniform PushConstant {
    mat4         view;
    mat4         projection;
    vec4         camera_position;
    DrawsBuffer  draws_buffer;
    MeshesBuffer meshes_buffer;
}
pc;

layout( location = 0 ) out vec3 color[];

struct MeshTaskPayload {
    uint meshlet_indices[32];
    uint draw_id;
};
taskPayloadSharedEXT MeshTaskPayload payload;

void main( ) {
    uint mid  = payload.meshlet_indices[gl_WorkGroupID.x];
    Draw draw = pc.draws_buffer.draws[payload.draw_id];

    Mesh    mesh    = pc.meshes_buffer.meshes[uint( draw.mesh )];
    Meshlet meshlet = mesh.meshlet_buffer.meshlets[mid];

    SetMeshOutputsEXT( meshlet.vertex_count, meshlet.triangle_count );

    for ( uint i = gl_LocalInvocationIndex; i < meshlet.vertex_count; i += gl_WorkGroupSize.x ) {
        uint   index = mesh.meshlet_vertices.vertices[meshlet.vertex_offset + i];
        Vertex v     = mesh.vertex_buffer.vertices[index];
        vec4   pos   = pc.projection * pc.view * draw.model * vec4( v.vx, v.vy, v.vz, 1 );
        color[i]     = vec3( v.nx, v.ny, v.nz ) * 0.5 + 0.5;

        gl_MeshVerticesEXT[i].gl_Position = pos;
    }

    for ( uint i = gl_LocalInvocationIndex; i < meshlet.triangle_count; i += gl_WorkGroupSize.x ) {
        gl_MeshPrimitivesEXT[i].gl_PrimitiveID = int( gl_WorkGroupID.x );
        uint offset                            = meshlet.triangle_offset + i * 3;
        gl_PrimitiveTriangleIndicesEXT[i]      = uvec3(
                mesh.meshlet_triangles.triangles[offset],
                mesh.meshlet_triangles.triangles[offset + 1],
                mesh.meshlet_triangles.triangles[offset + 2] );
    }
}